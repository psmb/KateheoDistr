prototype(Sfi.Kateheo:News) < prototype(Sfi.Kateheo:AbstractDocument) {
	body = Neos.Neos:ContentComponent {
		authorName = Neos.Fusion:Collection {
			collection = ${node.properties.authors}
			itemName = 'author'
			itemRenderer = Neos.Fusion:Tag {
				tagName = 'a'
				attributes.href = Neos.Neos:NodeUri {
					node = ${author}
				}
				content = ${String.stripTags(author.properties.title) + ' '}
				@if.setAuthor = ${author}
			}
		}

		authorInfo = Neos.Fusion:Collection {
			collection = ${node.properties.authors}
			itemName = 'author'
			itemRenderer = Sfi.Kateheo:News.AuthorInfo
		}

		cover = Neos.Neos:ContentCollection{
			nodePath = 'coverCollection'
			prototype(Neos.NodeTypes:Image){
				maximumWidth = 980
				maximumHeight = 653
				allowCropping = TRUE
			}
		}

		main = Neos.Neos:ContentCollection {
			nodePath = 'main'
		}

		@context.main = ${this.main}
		readingTimeIndicator = Sfi.Kateheo:ReadingTime {
			content = ${main}
		}

		date = ${Date.formatCldr(node.properties.date, 'dd MMMM yyyy')}

		nav = Neos.Fusion:Component {
			prev = Sfi.Kateheo:News.ShortForNav {
				mode = 'prev'
				@context.node = ${q(node).prev().get(0)}
			}
			next = Sfi.Kateheo:News.ShortForNav {
				mode = 'next'
				@context.node = ${q(node).next().get(0)}
			}
			renderer = afx`
				<div style="display:flex; margin: 8px -4px">
					<div class="" style="flex-basis: 50%; margin: 0 4px">{props.prev}</div>
					<div class="" style="flex-basis: 50%; margin: 0 4px; text-align: right">{props.next}</div>
				</div>
			`
		}

		sectionLink = Sfi.Kateheo:PrimarySection.Short {
			@context.node = ${q(node).parents('[instanceof Sfi.Kateheo:PrimarySection]').get(0)}
			@context.mode = 'news'
			@if.isInSection = ${node}
		}

		tagsList = Neos.Fusion:Collection {
			collection = ${node.properties.tags}
			itemName = 'node'
			itemRenderer = Neos.Fusion:Component {
				link = Neos.Neos:NodeUri {
					node = ${node}
				}
				title = ${node.properties.title}
				renderer = afx`
					<i class="fa fa-tag"></i>
					<a href={props.link}>
						<span itemprop="name">{props.title}</span>
					</a>
				`
				@if.notEmpty = ${node.properties.title}
			}
		}

		renderer = afx`
			<div class="Paper-Article">
				<div class="Article" style="margin-bottom: 60px">
					<div class="row">
						<div class="large-10 large-offset-1 columns">
							<div class="textAlign-center">
								<div class="Service--Large">{props.authorName}</div>
								<h1 class="H1 textAlign-center"><Neos.Neos:Editable property="title" block={false}/></h1>
								<div class="ServiceEm"><Neos.Neos:Editable property="teaser" block={false}/></div>
								<span class="ServiceEm" style="text-transform: lowercase;">{props.date} â€¢ <span class="color-grayLight">{props.readingTimeIndicator}</span></span>
							</div>
							<div class="color-primary"><Neos.Neos:Editable property="lead"/></div>
							{props.cover}
						</div>
					</div>
					<div class="row">
						<div class="large-8 medium-10 large-offset-2 medium-offset-1 columns">
							<div class="Article-Content">{props.main}</div>
							{props.authorInfo}
						</div>
					</div>
					<div>{props.tagsList}</div>
					{props.nav}
					{props.sectionLink}
				</div>
			</div>
		`
	}
}
