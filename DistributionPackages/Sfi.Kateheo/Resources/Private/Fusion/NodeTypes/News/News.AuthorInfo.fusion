prototype(Sfi.Kateheo:News.AuthorInfo) < prototype(Neos.Fusion:Component) {
    @if.notEmpty = ${author}
    authorName = ${String.stripTags(author.properties.title)}

    authorInfo = ${String.stripTags(author.properties.info)}

    imageUri = Neos.Neos:ImageUri {
        asset = ${author.properties.image}
        width = 100
        height = 100
        allowCropping = true
    }

    authorLink = Neos.Neos:NodeUri {
        node = ${author}
    }

    authorArticles = Sfi.Kateheo:News.AuthorInfoArticlesList

    renderer = afx`
        <div style="margin: 60px 0">
            <div class="H2 textAlign-center" style="margin: 36px 0">Об авторе</div>
            <div style="display: flex">
                <div @if.image={props.imageUri} class="marginRight-double" style="flex-shrink: 0">
                    <img style="border: 5px solid; border-color: var(--color-grayLine); border-radius: 50%" src={props.imageUri}/>
                </div>
                <div>
                    <a href={props.authorLink}>
                        <h4 class="H3">{props.authorName}</h4>
                        <p class="Paragraph marginTop-half">{props.authorInfo}</p>
                    </a>
                    <div class="marginTop-triple paddingTop-double">
                        {props.authorArticles}
                    </div>
                </div>
            </div>
        </div>
    `
}
