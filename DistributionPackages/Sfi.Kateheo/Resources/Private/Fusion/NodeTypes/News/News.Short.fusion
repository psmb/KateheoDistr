#Breif rendering of article, used in NewsList
prototype(Sfi.Kateheo:News.Short) < prototype(Neos.Neos:ContentComponent) {
	title = ${String.stripTags(node.properties.title)}
	nodeUri = Neos.Neos:NodeUri {
		node = ${node}
	}
	readingTimeIndicator = Sfi.Kateheo:ReadingTime {
		content = Neos.Neos:ContentCollection {
			nodePath = 'main'
		}
	}
	authors = Neos.Fusion:Collection {
		collection = ${node.properties.authors}
		itemName = 'author'
		itemRenderer = Neos.Fusion:Component {
			authorName = ${author.properties.title}
			authorInfo = ${author.properties.info}
			renderer = afx`
				<div class="H4">{props.authorName}</div>
				<div class="ServiceEm">{props.authorInfo}</div>
			`
		}
	}
	renderer = afx`
		<a href={props.nodeUri}>
			<div style="margin: 24px 0">
				{props.authors}
				<h3 class="Paragraph color-primary">{props.title}</h3>
				<div class="ParagraphAlt color-grayLight">{props.readingTimeIndicator}</div>
			</div>
		</a>
	`
}
