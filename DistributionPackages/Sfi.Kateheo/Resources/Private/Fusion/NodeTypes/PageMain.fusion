prototype(Sfi.Kateheo:PageMain) < prototype(Sfi.Kateheo:AbstractDocument) {
	prototype(Psmb.Carousel:Carousel).@process.wrap = afx`<div class="backgroundColor-kateheo">{value}</div>`
	body = Neos.Fusion:Component {
		main = Neos.Neos:ContentCollection{
			nodePath = 'main'
		}

		primarySections = Sfi.Kateheo:PrimarySections {
			mode = 'front'
		}

		newsList = Flowpack.Listable:Collection {
			@process.styles = ${'<style>
				.newsList {
					display: flex;
					flex-wrap: wrap;
					margin: -12px;
				}
				.newsList-item {
					flex-basis: 100%;
					padding: 12px;
				}
				@media screen and (min-width:640px) {
					.newsList-item {
						flex-basis: 50%;
					}
				}
				@media screen and (min-width:1024px) {
					.newsList-item {
						flex-basis: 33.33%;
					}
				}
				@media screen and (min-width:1024px) {
					.newsList-item:nth-child(4) {
						display: none;
					}
				}
			</style>' + value}
			listClass = 'newsList'
			itemClass = 'newsList-item'
			itemRenderer = Flowpack.Listable:ContentCaseShort
			collection = ${Search.query(site).nodeType("Sfi.Kateheo:News").sortDesc('date').from(0).limit(4).execute().toArray()}
		}

		soundCloudPlayer = Sfi.Kateheo:SoundCloudPlayer {
			soundCloudLink = ${site.properties.soundCloudLink}
		}

		renderer = afx`
			{props.style}
			<div style="display:flex; flex-direction: column">
				<div style="margin-bottom: 48px">
					{props.main}
				</div>
				<div class="primarySectionsMain" style="margin-bottom: 48px">{props.primarySections}</div>
				<div style="background-color: var(--color-grayLight); padding: 36px 24px; margin-bottom: 48px">
					<div class="row">
						<div class="columns medium-4">
							<h3 class="H2 color-white">Медиа</h3>
							<p class="ServiceEm--Large color-white" style="margin-top: 24px">Лекции в аудио формате</p>
						</div>
						<div class="columns medium-8">
							{props.soundCloudPlayer}
						</div>
					</div>
				</div>
				<h3 class="H2 color-grayLight" style="margin-bottom: 12px">Новости</h3>
				{props.newsList}
			</div>
		`
		style = ${'<style>
		@media screen and (max-width: 640px) {
			.primarySectionsMain {
				order: -1;
			}
		}
		</style>'}
	}
}
