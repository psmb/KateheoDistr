prototype(Sfi.Kateheo:AuthorsDirectoryPage) < prototype(Sfi.Kateheo:AbstractDocument) {
	body = Neos.Neos:ContentComponent {
		aMenu = Neos.Neos:Menu {
			itemCollection = ${q(site).find('[instanceof Sfi.Kateheo:Author]').get()}
			itemCollection.@process.sort = ${q(value).count() > 0 ? q(value).sort('title', 'ASC').get() : value}
			attributes.class = "nav nav-list bs-docs-sidenav affix-top"
			current.attributes.class = "active"
		}

		main = Neos.Neos:ContentCollection{
			nodePath = 'main'
		}

		newsList = Flowpack.Listable:List {
			@context.listClass = 'news-list-view'
			prototype(Flowpack.Listable:Listable) {
				@context.sortProperty = 'date'
				@context.sortOrder = 'DESC'
				@context.limit = 10
				collection = ${q(site).find('[instanceof Sfi.Kateheo:News]').filterByReference('authors', documentNode).get()}
			}
		}

		renderer = afx`
			<div class="row-fluid">
				<div class="span3">
					<div class="PageMain-authorMenu">{props.aMenu}</div>
				</div>
				<div class="span9" role="main">
					<div class="Category">
						<h1 class="Category-Title">
							<Neos.Neos:Editable property="title" block={false}/>
						</h1>
						<div class="Category-Content">{props.main}</div>
						<div class="Category-NewsList">{props.newsList}</div>
					</div>
				</div>
			</div>
		`
	}
}
