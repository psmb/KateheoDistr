prototype(Sfi.Kateheo:PrimarySection) < prototype(Sfi.Kateheo:AbstractDocument) {
	body = Neos.Neos:ContentComponent {
		title = ${node.properties.title}
		categoriesList = Neos.Fusion:Collection {
			collection = ${q(node).children('[instanceof Sfi.Kateheo:Category]').sort('_index', 'ASC').get()}
			itemName = 'category'
			itemRenderer = Neos.Fusion:Component {
				categoryTitle = ${category.properties.title}
				articlesList = Neos.Fusion:Collection {
					collection = ${q(category).children('[instanceof Sfi.Kateheo:Article]').sort('_index', 'ASC').get()}
					itemName = 'node'
					itemRenderer = Sfi.Kateheo:News.Short
				}
				renderer = afx`
					<div style="margin-bottom: 60px">
						<h2 class="H2">{props.categoryTitle}</h2>
						{props.articlesList}
					</div>
				`
			}
			@cache {
				mode = 'cached'
				entryIdentifier {
					node = ${node}
				}
			}
		}

		main = Neos.Neos:ContentCollection{
			nodePath = 'main'
		}

		renderer = afx`
			<div class="row js-sticky-main-content" style="margin-top: 36px">
				<div class="large-6 medium-10 columns js-sticky-sidebar" style="padding-left: 0">
					<div class="js-sticky-sidebar__inner" style="padding-bottom: 24px; padding-left: 12px">
						<h1 class="H1" style="margin-bottom: 48px">{props.title}</h1>
						{props.main}
					</div>
				</div>
				<div class="large-6 medium-10 columns end js-sticky-content">
					{props.categoriesList}
				</div>
			</div>
			<div style="margin: 48px 0">
				<Sfi.Kateheo:PrimarySections mode="section"/>
			</div>
		`
	}
}
