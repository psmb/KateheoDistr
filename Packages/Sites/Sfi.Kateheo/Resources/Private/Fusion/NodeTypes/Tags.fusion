prototype(Sfi.Kateheo:Tags) < prototype(Neos.Neos:ContentComponent) {
	aMenu = Neos.Neos:Menu {
		itemCollection = ${q(site).find('[instanceof Sfi.Kateheo:Tags]').get()}
		itemCollection.@process.sort = ${q(value).count() > 0 ? q(value).sort('title', 'ASC').get() : value}
		attributes.class = "nav nav-list bs-docs-sidenav affix-top"
		current.attributes.class = "active"
	}

	main = Neos.Neos:ContentCollection{
		nodePath = 'main'
	}

	newsList = Flowpack.Listable:List {
		@context.listClass = 'news-list-view'
		prototype(Flowpack.Listable:Listable) {
			@context.sortProperty = 'date'
			@context.sortOrder = 'DESC'
			@context.itemsPerPage = 50
			@context.paginationEnabled = ${true}
			collection = ${q(site).find('[instanceof Flowpack.Listable:ListableMixin]').filterByReference('tags', documentNode).get()}
		}
	}

	renderer = afx`
		<div class="row-fluid">
			<div class="span12" role="main">
				<div class="Tags">
					<h1 class="Tag-Name">
						Список материалов по тегу:
						<Neos.Neos:Editable property="title" block={false} />
					</h1>
					<div class="Tag-Content">{props.main}</div>
					<div class="Tag-NewsList">{props.newsList}</div>
				</div>
			</div>
		</div>
	`
}
