prototype(Sfi.Kateheo:PageMain) < prototype(Neos.Fusion:Component) {
	main = Neos.Neos:ContentCollection{
		nodePath = 'main'
	}
	publicationList = Flowpack.Listable:List {
		@context.listClass = 'publicaton-list-view'
		prototype(Flowpack.Listable:Listable) {
			collection = ${q(site).find('[instanceof Sfi.Kateheo:Category]').filter('[uriPathSegment = "publication"]').find('[instanceof Sfi.Kateheo:News]').get()}
		}
	}

	newsList = Flowpack.Listable:List {
		@context.listClass = 'news-list-view'
		prototype(Flowpack.Listable:Listable) {
			@context.limit = 20
			collection = ${q(site).find('[instanceof Sfi.Kateheo:Category]').filter('[uriPathSegment = "news"]').find('[instanceof Sfi.Kateheo:News]').get()}
		}
	}

	eventsList = Flowpack.Listable:List {
		@context.listClass = 'news-list-view'
		prototype(Flowpack.Listable:Listable) {
			@context.limit = 3
			collection = ${q(site).find('[instanceof Sfi.Kateheo:Category]').filter('[uriPathSegment = "news"]').find('[instanceof Sfi.Kateheo:News]').get()}
		}
	}

	leftMenu = Neos.Neos:Menu {
		itemCollection = ${q(site).children('[uriPathSegment = "missiologiia"]').add(q(site).children('[uriPathSegment = "katekhetika"]')).add(q(site).children('[uriPathSegment = "mission"]')).add(q(site).children('[uriPathSegment = "katekhizatsiia"]')).get()}
		attributes.class = "nav nav-list bs-docs-sidenav affix-top"
		maximumLevels = 1
		current.attributes.class = "active"
	}

	search = ${'
		<script>
			(function() {
				var cx = "001422831760821001445:WMX936016672";
				var gcse = document.createElement("script");
				gcse.type = "text/javascript";
				gcse.async = true;
				gcse.src = (document.location.protocol == "https:" ? "https:" : "http:") +
					"//cse.google.com/cse.js?cx=" + cx;
				var s = document.getElementsByTagName("script")[0];
				s.parentNode.insertBefore(gcse, s);
				})();
		</script>
		<gcse:search></gcse:search>
	'}

	renderer = afx`
	<div class="row-fluid">
		<div class="span3">
			<header class="csc-header csc-header-n1"><h1 class="csc-firstHeader">Теория и практика катехизации</h1></header>
			<div class="PageMain-leftMenu">{props.leftMenu}</div>
			<header class="csc-header csc-header-n1"><h1 class="csc-firstHeader">Мы в социальных сетях</h1></header>
			<div class="sn_icon" aria-hidden="true"><i class="fa fa-vk"></i><a target="_blank" href="http://vk.com/public54434887">Вконтакте</a></div>
			<div class="sn_icon" aria-hidden="true"><i class="fa fa-facebook"></i><a target="_blank" href="https://www.facebook.com/kateheo.ru">Facebook</a></div>
			<div class="sn_icon" aria-hidden="true"><i class="fa fa-pencil"></i><a target="_blank" href="http://idite-nau4ite.livejournal.com/">Живой Журнал</a></div>
			<header class="csc-header csc-header-n1"><h1 class="csc-firstHeader">Дайджест портала</h1></header>
			<div class="csc-textpic-text"><p><strong><a href="http://kateheo.ru/subscribe/">Подписаться</a></strong></p></div>
		</div>
		<div class="span6" role="main">
			<div class="PageMain">
				<header class="csc-header csc-header-n1"><h1 class="csc-firstHeader">Публикации</h1></header>
				<div class="PageMain-Content">{props.main}</div>
				<div class="PageMain-publicationList">{props.publicationList}</div>
			</div>
		</div>
		<div class="span3">
			<header class="csc-header csc-header-n1"><h1 class="csc-firstHeader">Поиск</h1></header>
			<div id="PageMain-serach">
				{props.search}
			</div>
			<header class="csc-header csc-header-n1"><h1 class="csc-firstHeader">Новости миссии и катехизации</h1></header>
			<div class="PageMain-newsList">{props.newsList}</div>
		</div>
	</div>
	`

}
